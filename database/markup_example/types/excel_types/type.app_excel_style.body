create or replace type body app#excel_style is
    
    constructor function app#excel_style 
    return self as result 
    is
    begin
        self.initialize_style;
        return;    
    end app#excel_style;
    
    constructor function app#excel_style
    (
        p_id   in varchar2,
        p_name in varchar2
    ) return self as result 
    is
    begin
    
        self.initialize_style;
        self.style_id   := p_id;
        self.style_name := p_name;
        return;
    end app#excel_style;
    
    member procedure initialize_style 
    is
    begin
        self.initialize;
        style_id      := null;
        style_name    := null;
        attributes    := new app#attribs_set();
        font          := new app#attribs_set();
        alignment     := new app#attribs_set();
        data_format   := new app#attribs_set();
        interior      := new app#attribs_set();
        border_top    := new app#attribs_set();
        border_bottom := new app#attribs_set();
        border_left   := new app#attribs_set();
        border_right  := new app#attribs_set();
        protection    := new app#attribs_set();
        style_xml     := null;
        dbms_lob.createtemporary(self.style_xml, false, dbms_lob.call);
    end initialize_style;
    
    /*
    <Style ss:ID="Default" ss:Name="Normal">
    <Alignment ss:Vertical="Bottom"/>
    <Borders/>
    or
    <Borders>
     <Border ss:Position="Bottom" ss:LineStyle="Continuous" ss:Weight="1"/>
     <Border ss:Position="Left" ss:LineStyle="Continuous" ss:Weight="1"/>
     <Border ss:Position="Right" ss:LineStyle="Continuous" ss:Weight="1"/>
     <Border ss:Position="Top" ss:LineStyle="Continuous" ss:Weight="1"/>
    </Borders>
    <Font/>
    or
    <Font x:Family="Swiss" ss:Bold="1"/>
    <Interior/>
    or
    <Interior ss:Color="#33CCCC" ss:Pattern="Solid"/>
    <NumberFormat/>
    or
    <NumberFormat ss:Format="&quot;$&quot;#,##0.00"/>
    or
    <NumberFormat ss:Format="Short Date"/>
    or
    <NumberFormat ss:Format="0"/>
    <Protection/>
    </Style>
    
    */
    member procedure set_style_attributes
    is
    begin
        self.attributes.add_attribute('ss:ID', self.style_id);
        self.attributes.add_attribute('ss:Name', self.style_name);
    end set_style_attributes;
        
    member procedure to_xml 
    is
        s_tag  varchar2(10) := 'Style';
        a_tag  varchar2(10) := 'Alignment';
        bs_tag varchar2(10) := 'Borders';
        b_tag  varchar2(10) := 'Border';
        f_tag  varchar2(10) := 'Font';
        i_tag  varchar2(10) := 'Interior';
        nf_tag varchar2(10) := 'NumberFormat';
        p_tag  varchar2(10) := 'Protection';
        l_temp varchar2(4000);
        
    v_inner_xml varchar2(32000);
    begin
    
        self.set_style_attributes;
    
        self.open(p_tag => s_tag, p_attributes => self.attributes.attributes_formatted);
        self.empty(p_tag => a_tag, p_attributes => self.alignment.attributes_formatted);

        if (self.border_top.attrib_count + self.border_bottom.attrib_count + self.border_left.attrib_count + self.border_right.attrib_count) = 0 then
            self.empty(p_tag => bs_tag);
        else
            self.open(p_tag => bs_tag);
            self.empty(p_tag => b_tag, p_attributes => self.border_top.attributes_formatted);
            self.empty(p_tag => b_tag, p_attributes => self.border_bottom.attributes_formatted);
            self.empty(p_tag => b_tag, p_attributes => self.border_left.attributes_formatted);
            self.empty(p_tag => b_tag, p_attributes => self.border_right.attributes_formatted);
            self.close(p_tag => bs_tag);
        end if;

        self.empty(p_tag => f_tag, p_attributes => self.font.attributes_formatted);
        self.empty(p_tag => i_tag, p_attributes => self.interior.attributes_formatted);
        self.empty(p_tag => nf_tag, p_attributes => self.data_format.attributes_formatted);
        self.empty(p_tag => p_tag, p_attributes => self.protection.attributes_formatted);

        self.close(s_tag);
        self.style_xml := self.data;
    
    end to_xml;
    
end;
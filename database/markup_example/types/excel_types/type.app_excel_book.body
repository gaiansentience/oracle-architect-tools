create or replace type body app#excel_book is
    
    constructor function app#excel_book 
    return self as result 
    is
    begin
        self.initialize_book;
        return;
    end app#excel_book;
    
    constructor function app#excel_book
    (
        p_author  in varchar2,
        p_company in varchar2
    ) return self as result 
    is
    begin
    
        self.initialize_book;
        self.author  := p_author;
        self.company := p_company;
        return;
    end app#excel_book;
    
    member procedure initialize_book 
    is
    begin
        self.initialize;
        self.tables     := new app#excel_tables();
        self.attributes := new app#attribs_set();
        self.styles     := new app#excel_styles_set();
        self.filename   := null;
        self.author     := null;
        self.created    := systimestamp;
        self.company    := null;
        self.version    := '10.6856';
        self.book_xml   := null;
        dbms_lob.createtemporary(self.book_xml, false, dbms_lob.call);
    end initialize_book;
    
    member procedure add_table(p_table in app#excel_table) 
    is
    begin
        if self.tables is null then
            self.tables := new app#excel_tables();
        end if;
        self.tables.extend;
        self.tables(self.tables.last) := p_table;
    end add_table;
    
    member procedure set_workbook_attributes 
    is
    begin
        /*
        <Workbook xmlns="" xmlns:o="" xmlns:x="" xmlns:ss="" xmlns:html="">
        */
        self.attributes.add_attribute('xmlns', 'urn:schemas-microsoft-com:office:spreadsheet');
        self.attributes.add_attribute('xmlns:o', 'urn:schemas-microsoft-com:office:office');
        self.attributes.add_attribute('xmlns:x', 'urn:schemas-microsoft-com:office:excel');
        self.attributes.add_attribute('xmlns:ss', 'urn:schemas-microsoft-com:office:spreadsheet');
        self.attributes.add_attribute('xmlns:html', 'http://www.w3.org/TR/REC-html40'); 
    end set_workbook_attributes;
    
    member procedure document_styles 
    is
    begin
        self.styles.to_xml;
        app#fmt.append_clob(self.data, self.styles.styles_xml);    
    end document_styles;
    
    member procedure document_properties 
    is
        l_attribute  app#attrib;
        dp_tag       varchar2(100) := 'DocumentProperties';
        a_tag        varchar2(100) := 'Author';
        la_tag       varchar2(100) := 'LastAuthor';
        c_tag        varchar2(100) := 'Created';
        co_tag       varchar2(100) := 'Company';
        v_tag        varchar2(100) := 'Version';
        l_fmt_date   varchar2(100) := 'YYYY-MM-DD';
        l_fmt_timetz varchar2(100) := 'HH24:MI:SS:ff TZH:TZM';
        ts           varchar2(1000);
    begin
        l_attribute := new app#attrib('xmlns', 'urn:schemas-microsoft-com:office:office');
        self.open(p_tag => dp_tag, p_attributes => l_attribute.attribute);
        self.element(p_value => self.author, p_tag => a_tag);
        self.element(p_value => self.author, p_tag => la_tag);
        ts := to_char(self.created, l_fmt_date) || 'T';
        ts := ts || to_char(self.created, l_fmt_timetz);
        self.element(p_value => ts, p_tag => c_tag);
        self.element(p_value => self.company, p_tag => co_tag);
        self.element(p_value => self.version, p_tag => v_tag);
        self.close(dp_tag);
    end document_properties;
    
    member procedure to_xml 
    is
        w_tag varchar2(100) := 'workbook';
    begin
        self.xml_declaration;
        self.set_workbook_attributes;
        self.open(p_tag => w_tag, p_attributes => self.attributes.attributes_formatted);
        self.document_properties;
        self.document_styles;
        for i in 1 .. self.tables.count loop
            self.tables(i).to_xml;
            app#fmt.append(self.data, self.tables(i).table_xml);
        end loop;
        self.close(p_tag => w_tag);
        self.book_xml := self.data;
    end to_xml;
    
end;
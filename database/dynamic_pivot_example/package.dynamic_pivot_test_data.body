create or replace package body dynamic_pivot_test_data
as

    pi constant number := 3.1415;
    g_load_3d_shapes boolean := false;

    type r_item_value_pair is record
        (
        value_id number, 
        value_name varchar2(100), 
        value_type varchar2(100), 
        value_data varchar2(4000)
        );
    
    type t_item_value_pairs is table of r_item_value_pair;

    type r_item is record
        (
        item_id number, 
        item_name varchar2(200), 
        item_type varchar2(100), 
        item_values t_item_value_pairs
        );
    
    type t_items is table of r_item;
    
    procedure set_load_3d_shapes
    (
        p_load_3d in boolean default false
    )
    is
    begin
    
        g_load_3d_shapes := p_load_3d;
    
    end set_load_3d_shapes;
    
    
        
    procedure log_err(p_msg in varchar2, p_locale in varchar2)
    is
    begin
        dbms_output.put_line('EXCEPTION IN ' || p_locale || ': ' || p_msg);
    end log_err;



    function get_shape_json
    (
        p_item in r_item
    ) return json_object_t
    is
        o_doc json_object_t := new json_object_t();
        a_o json_array_t := new json_array_t();
        ae_o json_object_t := new json_object_t();
    begin
    
        o_doc.put('itemId',p_item.item_id);
        o_doc.put('itemName', p_item.item_name);
        o_doc.put('itemType', p_item.item_type);
        o_doc.put('itemValuePairs', a_o);
        a_o := o_doc.get_array('itemValuePairs');
        for i in p_item.item_values.first..p_item.item_values.last loop
            ae_o := new json_object_t();
            ae_o.put('valueId', p_item.item_values(i).value_id);
            ae_o.put('valueName', p_item.item_values(i).value_name);
            ae_o.put('valueType', p_item.item_values(i).value_type);
            ae_o.put('valueData', p_item.item_values(i).value_data);
            a_o.append(ae_o);
        end loop;
        o_doc.put('itemValuePairsAdded', a_o.get_size());
    
        return o_doc;

    exception
        when others then
            log_err(sqlerrm,'get_shape_json');
            raise;
    end get_shape_json;
    
    function get_all_shapes_json
    (
        p_items in out nocopy t_items
    ) return json_object_t
    is
        o_doc json_object_t := new json_object_t();
        a_o json_array_t := new json_array_t();
        ae_o json_object_t := new json_object_t();
    begin
    
        o_doc.put('itemAssortment', 'shapes');
        o_doc.put('items', a_o);
        a_o := o_doc.get_array('items');
        for i in p_items.first..p_items.last loop
            ae_o := get_shape_json(p_items(i));
            a_o.append(ae_o);
        end loop;
        o_doc.put('itemsAdded', a_o.get_size());
        
        return o_doc;

    exception
        when others then
            log_err(sqlerrm,'get_all_shapes_json');
            raise;    
    end get_all_shapes_json;

    function define_item
    (
        p_item_id in number, 
        p_item_name in varchar2, 
        p_item_type in varchar2
    ) return r_item
    is
        l_item r_item;
    begin
        l_item.item_id := p_item_id;
        l_item.item_name := p_item_name || '-' || p_item_id;
        l_item.item_type := p_item_type;
        l_item.item_values := new t_item_value_pairs();
        return l_item;
        
    exception
        when others then
            log_err(sqlerrm,'define_item');
            raise;
    end define_item;
        
    function define_value_pair
    (
        p_value_id in number, 
        p_value_name in varchar2, 
        p_value_type varchar2, 
        p_value_data in varchar2
    ) return r_item_value_pair
    is
        l_vp r_item_value_pair;
    begin
        l_vp.value_id := p_value_id;
        l_vp.value_name := p_value_name;
        l_vp.value_type := p_value_type;
        l_vp.value_data := p_value_data;
        return l_vp;
        
    exception
        when others then
            log_err(sqlerrm,'define_value_pair');
            raise;
    end define_value_pair;
    
    procedure append_value_pair
    (
        p_item in out nocopy r_item, 
        p_value_id in number, 
        p_value_name in varchar2, 
        p_value_type varchar2, 
        p_value_data in varchar2
    )
    is
        l_vp r_item_value_pair;
    begin
        l_vp := define_value_pair(p_value_id, p_value_name, p_value_type, p_value_data);
        p_item.item_values.extend();
        p_item.item_values(p_item.item_values.last) := l_vp;
        
    exception
        when others then
            log_err(sqlerrm,'append_value_pair');
            raise;
    end append_value_pair;

    procedure append_shape
    (
        p_items in out nocopy t_items, 
        p_shape in r_item
    )
    is
    begin
        p_items.extend;
        p_items(p_items.last) := p_shape;
        
    exception
        when others then
            log_err(sqlerrm,'append_shape');
            raise;
    end append_shape;

    function right_triangle_hypotenuse
    (
        p_side_a in number,
        p_side_b in number
    ) return number
    is
        l_side_c number;
    begin
        --c2 = a2 + b2
        l_side_c := power( (power(p_side_a,2) + power(p_side_b,2))  , 0.5);
        return round(l_side_c, 4);
    end right_triangle_hypotenuse;
    
    function right_triangle_side
    (
        p_side_a in number,
        p_hypotenuse in number
    ) return number
    is
        l_side_b number;
    begin
        --c2 = a2 + b2
        --b2 = c2 - a2
        --b = sqrt(c - a)
        l_side_b := power( (p_hypotenuse - p_side_a)  , 0.5);
        return round(l_side_b, 4);
    end right_triangle_side;
    
    function triangle_height
    (
        p_base in number,
        p_a_degress in number,
        p_b_degrees in number
    ) return number
    is
        l_height number;
    begin
        raise_application_error(-20100, 'triangle height is undefined');    
        return round(l_height, 4);
    end triangle_height;
    
    function triangle_area
    (
        p_base in number,
        p_height in number
    ) return number
    is
        l_area number;
    begin
        l_area := 1/2 * p_base * p_height;
        return round(l_area, 4);
    end triangle_area;
    
    function equilateral_triangle_area
    (
        p_side in number
    ) return number
    is
        l_area number;
        l_angles number := 60;
        l_height number := triangle_height(p_side, l_angles, l_angles);
    begin
        l_area := triangle_area(p_side, l_height);
        return round(l_area, 4);
    end equilateral_triangle_area;
    
    function right_triangle_area
    (
        p_base in number,
        p_height in number
    ) return number
    is
        l_area number;
    begin
        l_area := triangle_area(p_base, p_height);
        return round(l_area, 4);    
    end right_triangle_area;
    
    function triangle_side
    (
        p_side_a in number,
        p_side_b in number,
        p_angle in number
    ) return number
    is
        l_side_c number;
    begin
        raise_application_error(-20100, 'triangle side c given side a and side b and angle is undefined');
        return round(l_side_c, 4);
    end triangle_side;
    
    function get_triangle
    (
        p_id in number,
        p_name in varchar2,
        p_a_degrees in number,
        p_b_degrees in number,
        p_a_side in number,
        p_b_side in number
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'triangle'); 
        l_c_degrees number := 180 - (p_a_degrees + p_b_degrees);
        --pretend its a right triangle for side and area
        l_c_side number := right_triangle_hypotenuse(p_a_side, p_b_side);
        l_area number := triangle_area(p_base => p_a_side, p_height => p_b_side);
    begin
        append_value_pair(l_item, p_id + 0.1, 'a_degrees', 'number', p_a_degrees);
        append_value_pair(l_item, p_id + 0.2, 'b_degrees', 'number', p_b_degrees);
        append_value_pair(l_item, p_id + 0.3, 'c_degrees', 'number', l_c_degrees);
        append_value_pair(l_item, p_id + 0.4, 'a_side', 'number', p_a_side);
        append_value_pair(l_item, p_id + 0.5, 'b_side', 'number', p_b_side);
        append_value_pair(l_item, p_id + 0.6, 'c_side', 'number', l_c_side);
        append_value_pair(l_item, p_id + 0.7, 'area', 'number', l_area);
        return l_item;
    end get_triangle;
    
    function get_square
    (
        p_id in number,
        p_name in varchar2,
        p_side in number
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'square'); 
        l_area number := power(p_side, 2);
    begin
        append_value_pair(l_item, p_id + 0.1, 'side', 'number', p_side);
        append_value_pair(l_item, p_id + 0.2, 'area', 'number', l_area);
        return l_item;
    end get_square;

    function get_rectangle
    (
        p_id in number,
        p_name in varchar2,
        p_length in number,
        p_width in number,
        p_color in varchar2
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'rectangle'); 
        l_area number := p_length * p_width;
    begin
        append_value_pair(l_item, p_id + 0.1, 'length', 'number', p_length);
        append_value_pair(l_item, p_id + 0.2, 'width', 'number', p_width);
        append_value_pair(l_item, p_id + 0.3, 'area', 'number', l_area);
        append_value_pair(l_item, p_id + 0.4, 'color', 'varchar2', p_color);
        return l_item;
    end get_rectangle;
    
    function circumference
    (
        p_radius in number
    ) return number
    is
        l_circumference number;
    begin
        l_circumference := 2 * pi * p_radius;
        l_circumference := round(l_circumference, 4);
        return l_circumference;
    end circumference;
    
    function circle_area
    (
        p_radius in number
    ) return number
    is
        l_area number;
    begin
        l_area := pi * power(p_radius,2);
        return round(l_area,4);
    end circle_area;

    function get_circle
    (
        p_id in number,
        p_name in varchar2,
        p_radius in number,
        p_color in varchar2
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'circle'); 
        l_area number := circle_area(p_radius);
        l_circumference number := circumference(p_radius);
    begin
        append_value_pair(l_item, p_id + 0.1, 'radius', 'number', p_radius);
        append_value_pair(l_item, p_id + 0.2, 'circumference', 'number', l_circumference);
        append_value_pair(l_item, p_id + 0.3, 'area', 'number', l_area );
        append_value_pair(l_item, p_id + 0.4, 'color', 'varchar2', p_color);
        return l_item;
    end get_circle;

    function sphere_volume
    (
        p_radius in number
    ) return number
    is
        l_volume number;
    begin
        l_volume := 4/3 * pi * power(p_radius, 3);
        l_volume := round(l_volume, 4);
        return l_volume;
    end sphere_volume;

    function sphere_surface_area
    (
        p_radius in number
    ) return number
    is
        l_area number;
    begin
        l_area := 4 * pi * power(p_radius, 2);
        l_area := round(l_area, 4);
        return l_area;
    end sphere_surface_area;

    function get_sphere
    (
        p_id in number,
        p_name in varchar2,
        p_radius in number,
        p_color in varchar2
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'sphere'); 
        l_circumference number := circumference(p_radius);
        l_surface_area number := sphere_surface_area(p_radius);
        l_volume number := sphere_volume(p_radius);
    begin
        append_value_pair(l_item, p_id + 0.1, 'radius', 'number', p_radius);
        append_value_pair(l_item, p_id + 0.2, 'circumference', 'number', l_circumference);
        append_value_pair(l_item, p_id + 0.3, 'surfaceArea', 'number', l_surface_area);
        append_value_pair(l_item, p_id + 0.4, 'volume', 'number', l_volume);
        append_value_pair(l_item, p_id + 0.5, 'color', 'varchar2', p_color);
        return l_item;
    end get_sphere;
    
    
    function get_cube
    (
        p_id in number,
        p_name in varchar2,
        p_side in number,
        p_composition in varchar2
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'cube');
        l_surface_area number := 6 * power(p_side, 2);
        l_volume number := power(p_side, 3);
    begin
        append_value_pair(l_item, p_id + 0.1, 'side', 'number', p_side);
        append_value_pair(l_item, p_id + 0.2, 'surfaceArea', 'number', l_surface_area);
        append_value_pair(l_item, p_id + 0.3, 'volume', 'number', l_volume);        
        append_value_pair(l_item, p_id + 0.4, 'composition', 'varchar2', p_composition);
        return l_item;
    end get_cube;

    
    function get_cylinder
    (
        p_id in number,
        p_name in varchar2,
        p_radius in number,
        p_height in number
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'cylinder');
        l_base_circumference number := circumference(p_radius);
        l_base_area number := circle_area(p_radius);
        l_curved_area number := l_base_circumference * p_height;
        l_total_surface_area number := l_curved_area + l_base_area * 2;
        l_volume number := l_base_area * p_height;
    begin
        append_value_pair(l_item, p_id + 0.1, 'radius', 'number', p_radius);
        append_value_pair(l_item, p_id + 0.2, 'height', 'number', p_height);
        append_value_pair(l_item, p_id + 0.3, 'baseCircumference', 'number', l_base_circumference);
        append_value_pair(l_item, p_id + 0.4, 'baseArea', 'number', l_base_area);
        append_value_pair(l_item, p_id + 0.5, 'curvedArea', 'number', l_curved_area);
        append_value_pair(l_item, p_id + 0.6, 'totalSurfaceArea', 'number', l_total_surface_area);
        append_value_pair(l_item, p_id + 0.7, 'volume', 'number', l_volume);
        return l_item;        
    end get_cylinder;
    
    
    
    function conic_lateral_area
    (
        p_radius in number,
        p_slant_height in number
    ) return number
    is
        l_area number;
    begin
        l_area := pi * p_radius * p_slant_height;
        return round(l_area, 4);
    end conic_lateral_area;
    
    function conic_volume
    (
        p_radius in number,
        p_height in number
    ) return number
    is
        l_volume number;
    begin
        l_volume := 1/3 * pi * power(p_radius, 2) * p_height;
        return round(l_volume, 4);
    end conic_volume;
    
    function get_conic
    (
        p_id in number,
        p_name in varchar2,
        p_radius in number,
        p_height in number    
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'conic');
        l_volume number := conic_volume(p_radius, p_height);
        l_base_circumference number := circumference(p_radius);
        l_base_area number := circle_area(p_radius);
        l_slant_height number := right_triangle_hypotenuse(p_radius, p_height);
        l_lateral_area number := conic_lateral_area(p_radius, l_slant_height);
        l_total_surface_area number := l_lateral_area + l_base_area;    
    begin
        append_value_pair(l_item, p_id + 0.1, 'radius', 'number', p_radius);
        append_value_pair(l_item, p_id + 0.2, 'height', 'number', p_height);
        append_value_pair(l_item, p_id + 0.3, 'baseCircumference', 'number', l_base_circumference);
        append_value_pair(l_item, p_id + 0.4, 'baseArea', 'number', l_base_area);
        append_value_pair(l_item, p_id + 0.4, 'slantHeight', 'number', l_slant_height);
        append_value_pair(l_item, p_id + 0.5, 'lateralArea', 'number', l_lateral_area);
        append_value_pair(l_item, p_id + 0.6, 'totalSurfaceArea', 'number', l_total_surface_area);
        append_value_pair(l_item, p_id + 0.7, 'volume', 'number', l_volume);    
        return l_item;        
    end get_conic;

    
    function cuboid_surface_area
    (
        p_base in number, 
        p_depth in number, 
        p_height in number    
    ) return number
    is
        l_surface_area number;
    begin
        l_surface_area := (2 * p_base * p_depth) + (2 * p_base * p_height) + (2 * p_depth * p_height);
        return round(l_surface_area, 4);
    end cuboid_surface_area;
    
    function get_cuboid
    (
        p_id in number,
        p_name in varchar2,    
        p_base in number, 
        p_depth in number, 
        p_height in number
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'conic');
        l_volume number := p_base * p_depth * p_height;
        l_surface_area number := cuboid_surface_area(p_base, p_depth, p_height);
    begin
        append_value_pair(l_item, p_id + 0.1, 'base', 'number', p_base);
        append_value_pair(l_item, p_id + 0.2, 'depth', 'number', p_depth);
        append_value_pair(l_item, p_id + 0.3, 'height', 'number', p_height);
        append_value_pair(l_item, p_id + 0.4, 'surfaceArea', 'number', l_surface_area);
        append_value_pair(l_item, p_id + 0.5, 'volume', 'number', l_volume);    
        return l_item;        
    end get_cuboid;


    function cosecant
    (
        p_angle in number
    ) return number
    is
        l_cosecant number;
    begin
        l_cosecant := 1/sin(p_angle);
        return l_cosecant;
    end cosecant;
    
    function polygon_circumradius
    (
        p_sides in number,
        p_length in number
    ) return number
    is
        l_circumradius number;
    begin
        l_circumradius := 1/2 * p_length * cosecant(pi/p_sides);
        return round(l_circumradius, 4);
    end polygon_circumradius;

    function polygon_interior_angle
    (
        p_sides in number
    ) return number
    is
        l_angle number;
    begin
        l_angle := 180 * (p_sides - 2) / p_sides;
        return round(l_angle, 4);
    exception
        when zero_divide then
            return 0;
    end polygon_interior_angle;
    
    function polygon_area
    (
        p_sides in number,
        p_length in number
    ) return number
    is
        l_area number;
    begin
        l_area := p_sides * power(p_length,2) * (cos(pi/p_sides)/sin(pi/p_sides))/4;
        return round(l_area, 4);
    end polygon_area;
    
    function get_polygon
    (
        p_id in number,
        p_name in varchar2,
        p_sides in number,
        p_side_length in number,
        p_shading in varchar2
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'regular polygon'); 
        l_interior_angles number := polygon_interior_angle(p_sides);        
        l_sum_angles number := l_interior_angles * p_sides;
        l_area number := polygon_area(p_sides, p_side_length);
    begin
        append_value_pair(l_item, p_id + 0.1, 'sides', 'number', p_sides);
        append_value_pair(l_item, p_id + 0.2, 'sideLength', 'number', p_side_length);
        append_value_pair(l_item, p_id + 0.3, 'interiorAngles', 'number', l_interior_angles);
        append_value_pair(l_item, p_id + 0.4, 'sumInteriorAngles', 'number', l_sum_angles);
        append_value_pair(l_item, p_id + 0.5, 'area', 'number', l_area);
        append_value_pair(l_item, p_id + 0.6, 'shading', 'varchar2', p_shading);
        return l_item;       
    end get_polygon;

    
    function pyramid_base_angles
    (   
        p_height in number,
        p_base_length in number,
        p_base_sides in number
    ) return number
    is
        l_angle number;
    begin
        raise_application_error(-20100, 'Pyramid base angle is undefined');
        return round(l_angle, 4);
    end pyramid_base_angles;

    function pyramid_vertex_angles
    (   
        p_height in number,
        p_base_length in number,
        p_base_sides in number
    ) return number
    is
        l_angle number;
    begin
        raise_application_error(-20100, 'Pyramid vertex angle is undefined');
        return round(l_angle, 4);
    end pyramid_vertex_angles;
    
    function pyramid_slant_height
    (
        p_base in number,
        p_height in number,
        p_sides in number
    ) return number
    is
        l_slant_height number;
        l_circumradius number := polygon_circumradius(p_sides,p_base);
    begin
        case p_sides
            when 4 then
                l_slant_height := right_triangle_hypotenuse(p_base / 2, p_height);
            when 3 then
                l_slant_height := power(p_base, 2) * 1/12;
            else
                --slant_height = sqrt(h2 + circumradius2)
                l_slant_height := power(  (power(p_height,2) + power(l_circumradius, 2))  , 0.5);
        end case;
        return round(l_slant_height, 4);
    end pyramid_slant_height;
    
    
    function pyramid_base_area
    (
        p_base_length in number,
        p_base_sides in number
    ) return number
    is
        l_area number;
    begin
        case
        when p_base_sides = 4 then
            l_area := power(p_base_length, 2);
        when p_base_sides = 3 then
            l_area := equilateral_triangle_area(p_base_length);
        else
            l_area := polygon_area(p_base_length, p_base_sides);
        end case;
        return round(l_area, 4);
     end pyramid_base_area;
    
    function pyramid_side_area
    (
        p_base_length in number,
        p_slant_height in number
    ) return number
    is
        l_area number;
    begin
        l_area := 1/2 * p_base_length * p_slant_height;
        return round(l_area);
    end pyramid_side_area;

    function pyramid_volume
    (   
        p_height in number,
        p_base_area in number
    ) return number
    is
        l_volume number;
    begin
        --height × base area / 3.
        l_volume := 1/3 * p_height * p_base_area;
        return round(l_volume, 4);    
    end pyramid_volume;
    
    function get_pyramid
    (
        p_id in number,
        p_name in varchar2,
        p_height in number,
        p_base_length in number,
        p_base_sides in number
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'pyramid');
        --l_base_angles number := pyramid_base_angles(p_height, p_base_length, p_base_sides);
        --l_vertex_angles number := pyramid_vertex_angles(p_height, p_base_length, p_base_sides);
        l_base_perimeter number := p_base_length * p_base_sides;
        l_base_area number := pyramid_base_area(p_base_length, p_base_sides);
        l_slant_height number := pyramid_slant_height(p_base_length, p_height, p_base_sides);
        l_side_area number := pyramid_side_area(p_base_length, l_slant_height);
        l_total_side_area number := l_side_area * p_base_sides;
        l_total_surface_area number := l_total_side_area + l_base_area;        
        l_volume number := pyramid_volume(p_height, l_base_area);
    begin
        append_value_pair(l_item, p_id + 0.1, 'height', 'number', p_height);
        append_value_pair(l_item, p_id + 0.2, 'baseLength', 'number', p_base_length);
        append_value_pair(l_item, p_id + 0.3, 'baseSides', 'number', p_base_sides);
        append_value_pair(l_item, p_id + 0.4, 'baseArea', 'number', l_base_area);
        append_value_pair(l_item, p_id + 0.5, 'basePerimeter', 'number', l_base_perimeter);
        --append_value_pair(l_item, p_id + 0.6, 'baseAngles', 'number', l_base_angles);
        --append_value_pair(l_item, p_id + 0.7, 'vertexAngles', 'number', l_vertex_angles);
        append_value_pair(l_item, p_id + 0.8, 'slantHeight', 'number', l_slant_height);
        append_value_pair(l_item, p_id + 0.9, 'sideArea', 'number', l_side_area);
        append_value_pair(l_item, p_id + 0.11, 'totalSideArea', 'number', l_total_side_area);
        append_value_pair(l_item, p_id + 0.12, 'totalSurfaceArea', 'number', l_total_surface_area);
        append_value_pair(l_item, p_id + 0.13, 'volume', 'number', l_volume);   
        return l_item;        
    end get_pyramid;
    

    procedure get_cubes(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_cube(p_items.count + 1, 'cube 1', p_side => 4, p_composition => 'glass'));
        append_shape(p_items, get_cube(p_items.count + 1, 'cube 2', p_side => 8, p_composition => 'calcite'));
    
    exception
        when others then
            log_err(sqlerrm,'get_cubes');
            raise;
    end get_cubes;

    procedure get_cuboids(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_cuboid(p_items.count + 1, 'cuboid 1', p_base => 4, p_depth => 2, p_height => 1));
        append_shape(p_items, get_cuboid(p_items.count + 1, 'cuboid 2', p_base => 8, p_depth => 3, p_height => 2));
    
    exception
        when others then
            log_err(sqlerrm,'get_cuboids');
            raise;
    end get_cuboids;

    procedure get_spheres(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_sphere(p_items.count + 1, 'sphere 1', p_radius => 4, p_color => 'silver'));
        append_shape(p_items, get_sphere(p_items.count + 1, 'sphere 2', p_radius => 8, p_color => 'bronze'));
    
    exception
        when others then
            log_err(sqlerrm,'get_spheres');
            raise;
    end get_spheres;


    procedure get_cylinders(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_cylinder(p_items.count + 1, 'cylinder 1', p_radius => 4, p_height => 1));
        append_shape(p_items, get_cylinder(p_items.count + 1, 'cylinder 2', p_radius => 8, p_height => 2));
    
    exception
        when others then
            log_err(sqlerrm,'get_cylinders');
            raise;
    end get_cylinders;




    procedure get_conics(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_conic(p_items.count + 1, 'conic 1', p_radius => 4, p_height => 1));
        append_shape(p_items, get_conic(p_items.count + 1, 'conic 2', p_radius => 8, p_height => 2));
    
    exception
        when others then
            log_err(sqlerrm,'get_conics');
            raise;
    end get_conics;


    procedure get_pyramids(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_pyramid(p_items.count + 1, 'pyramid 1', p_height => 22, p_base_length => 10, p_base_sides => 4));
        append_shape(p_items, get_pyramid(p_items.count + 1, 'pyramid 2', p_height => 33, p_base_length => 100, p_base_sides => 5));
    
    exception
        when others then
            log_err(sqlerrm,'get_pyramids');
            raise;
    end get_pyramids;
    
    
    function get_line
    (
        p_id in number,
        p_name in varchar2,
        p_slope in number,
        p_y_intercept in number
    ) return r_item
    is
        l_item r_item := define_item(p_id, p_name, 'line');
        l_equation varchar2(100) := 'y = ' || p_slope || 'x + ' || p_y_intercept;
    begin
        append_value_pair(l_item, p_id + 0.1, 'y_intercept', 'number', p_y_intercept);
        append_value_pair(l_item, p_id + 0.2, 'slope', 'number', p_slope);
        append_value_pair(l_item, p_id + 0.3, 'equation', 'varchar2', l_equation);
        return l_item;    
    end get_line;

    procedure get_lines(p_items in out nocopy t_items)
    is
    begin

        append_shape(p_items, get_line(p_items.count + 1, 'line 1', 3/4, 3));
        append_shape(p_items, get_line(p_items.count + 1, 'line 2', 2/5, -2));    
    
    exception
        when others then
            log_err(sqlerrm,'get_lines');
            raise;
    end get_lines;

    procedure get_polygons(p_items in out nocopy t_items)
    is
    begin
        append_shape(p_items, get_polygon(p_items.count + 1, 'triangle', 3, 2, 'teal'));
        append_shape(p_items, get_polygon(p_items.count + 1, 'square', 4, 2, 'blue'));
        append_shape(p_items, get_polygon(p_items.count + 1, 'pentagon', 5, 4, 'yellow'));
        append_shape(p_items, get_polygon(p_items.count + 1, 'hexagon', 6, 3, 'red'));
        append_shape(p_items, get_polygon(p_items.count + 1, 'nonagon', 9, 2.5, 'purple'));
        append_shape(p_items, get_polygon(p_items.count + 1, 'undecagon', 11, 4, 'pink'));
        
    exception
        when others then
            log_err(sqlerrm,'get_polygons');
            raise;
    end get_polygons;


    procedure get_circles(p_items in out nocopy t_items)
    is
    begin
        append_shape(p_items, get_circle(p_items.count + 1, 'first circle', 1,'blue'));
        append_shape(p_items, get_circle(p_items.count + 1, 'second circle', 5,'green'));
        
    exception
        when others then
            log_err(sqlerrm,'get_circles');
            raise;        
    end get_circles;
    
    procedure get_rectangles(p_items in out nocopy t_items)
    is
    begin
        append_shape(p_items, get_rectangle(p_items.count + 1, 'first rectangle', 8, 2.5, 'coral'));
        append_shape(p_items, get_rectangle(p_items.count + 1, 'second rectangle', 5, 7, 'turquoise'));
        
    exception
        when others then
            log_err(sqlerrm,'get_rectangles');
            raise;        
    end get_rectangles;

    
    procedure get_squares(p_items in out nocopy t_items)
    is
    begin
        append_shape(p_items, get_square(p_items.count + 1, 'first square', 4));
        append_shape(p_items, get_square(p_items.count + 1, 'second square', 3));
        
    exception
        when others then
            log_err(sqlerrm,'get_squares');
            raise;
    end get_squares;

    procedure get_triangles(p_items in out nocopy t_items)
    is
    begin
        append_shape(p_items, get_triangle(p_items.count + 1, 'first triangle', 30,60, 2,4));
        append_shape(p_items, get_triangle(p_items.count + 1, 'second triangle', 45,45, 3,6));
        append_shape(p_items, get_triangle(p_items.count + 1, 'third triangle', 40,60, 5,7));

    exception
        when others then
            log_err(sqlerrm,'get_triangles');
            raise;
    end get_triangles;
    
    function get_shapes
    (
        p_iterations in number default 1
    ) return t_items
    is
        l_items t_items := new t_items();
    begin

        for i in 1..p_iterations loop
            get_triangles(l_items);
            get_squares(l_items);
            get_rectangles(l_items);
            get_circles(l_items);
            get_polygons(l_items);
            get_lines(l_items);


            if g_load_3d_shapes then

                get_cubes(l_items);
                get_cuboids(l_items);
                get_spheres(l_items);
                get_cylinders(l_items);
                get_conics(l_items);
                get_pyramids(l_items);

            end if;
            
        end loop;
        
        return l_items;
        
    exception
        when others then
            log_err(sqlerrm,'get_shapes');
            raise;
    end get_shapes;

    function get_json_data
    (
        p_iterations in number default 1
    )
    return clob
    is
        l_clob clob;
        l_pretty clob;
        l_doc json_object_t;
        l_items t_items;
    begin
        l_items := get_shapes(p_iterations);
        l_doc := get_all_shapes_json(l_items);
        l_clob := l_doc.to_clob;
        
        select json_serialize(l_clob returning clob pretty) into l_pretty from dual;
        
        return l_pretty;

    exception
        when others then
            log_err(sqlerrm,'get_json_data');
            raise;
    end get_json_data;

begin
    dbms_random.seed('Albert Einstein');
end dynamic_pivot_test_data;
